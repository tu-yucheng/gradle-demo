// 构建Gradle脚本自身需要的资源，可以声明的资源包括依赖项、第三方插件、maven仓库地址等。
buildscript {
    ext {
        assertj = '3.21.0'
        junitJupiter = '5.9.2'
        junitPlatform = '1.9.2'
        junit4 = '4.13.2'
        hamcrest = '2.2'
        hamcrestAll = '1.3'
        mockito = '4.4.0'
    }

    repositories {
        maven { url 'https://maven.aliyun.com/nexus/content/groups/public/' }
        maven { url 'https://repo.spring.io/milestone' }
    }
}

plugins {
    id 'java'
    id "jacoco"
    id "org.sonarqube" version '3.5.0.2730'
}

sonar {
    properties {
        property "sonar.projectKey", "tu-yucheng_gradle-demo"
        property "sonar.organization", "tuyucheng"
        property "sonar.host", "https://sonarcloud.io"
        property "sonar.sourceEncoding", "UTF-8"
    }
}

description 'Java全栈教程'

// 全局配置，作用于所有子模块
allprojects {
    group project_group
    version project_version

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

    repositories {
        maven { url 'https://maven.aliyun.com/nexus/content/groups/public/' }
        maven { url 'https://repo.spring.io/milestone' }
        maven { url 'https://repo.maven.apache.org/maven2/' }
        mavenCentral()
    }
}

apply from: 'dependencies.gradle'
def env = System.getProperty("dev") ?: 'dev'

// 配置所有子项目
subprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'jacoco'

    sourceCompatibility = project_jdk
    targetCompatibility = project_jdk

    // 公共依赖
    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
        testImplementation 'org.junit.jupiter:junit-jupiter-params:5.9.2'
        testImplementation 'org.junit.vintage:junit-vintage-engine:5.9.2'
        testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.9.2'
        testImplementation 'org.assertj:assertj-core:3.21.0'
        testImplementation 'org.hamcrest:hamcrest:2.2'
        testImplementation 'org.hamcrest:hamcrest-all:1.3'
        testImplementation 'org.mockito:mockito-core:4.4.0'
    }

    sourceSets {
        main {
            java { srcDirs = ['src/main/java'] }
            resources { srcDirs = ['src/main/resources', "src/main/profiles/$env"] }
        }
        test {
            java { srcDirs = ['src/test/java', 'src/test/groovy'] }
            resources { srcDirs = ['src/test/resources'] }
        }
    }

    test {
        useJUnitPlatform()
        finalizedBy jacocoTestReport // report is always generated after tests run
    }

    jacocoTestReport {
        dependsOn test // tests are required to run before generating the report
        reports {
            xml.required = true
        }
    }

    jacoco {
        toolVersion = '0.8.8'
    }

    [compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'
}