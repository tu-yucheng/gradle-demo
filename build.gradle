// 构建Gradle脚本自身需要的资源，可以声明的资源包括依赖项、第三方插件、maven仓库地址等。
buildscript {
    ext {
        springboot = '2.7.8'
        spring = '5.3.25'
        h2 = '2.1.214'
        lombok = '1.18.24'
        aspectjweaver = '1.9.1'

        assertj = '3.21.0'
        junitJupiter = '5.9.2'
        junitPlatform = '1.9.2'
        junit4 = '4.13.2'
        hamcrest = '2.2'
        hamcrestAll = '1.3'
        mockito = '4.4.0'
        jmh = '1.35'

        commonsCollections4 = '4.4'
        commonsIo = '2.11.0'
        commonsLang = '2.6'
        commonsLang3 = '3.12.0'
        commonsCli = '1.4'
        commonsFileupload = '1.4'
    }

    repositories {
        maven { url 'https://maven.aliyun.com/nexus/content/groups/public/' }
        maven { url 'https://repo.spring.io/milestone' }
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springboot}")
    }
}

plugins {
    id 'java'
    id 'org.springframework.boot' version '2.6.1'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id "jacoco"
    id "org.sonarqube" version '3.5.0.2730'
}

sonar {
    properties {
        property "sonar.projectKey", "tu-yucheng_gradle-demo"
        property "sonar.organization", "tuyucheng"
        property "sonar.host", "https://sonarcloud.io"
    }
}

description 'Java全栈教程'

// 全局配置，作用于所有子模块
allprojects {
    group project_group
    version project_version

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

    repositories {
        maven { url 'https://maven.aliyun.com/nexus/content/groups/public/' }
        maven { url 'https://repo.spring.io/milestone' }
        maven { url 'https://repo.maven.apache.org/maven2/' }
        mavenCentral()
    }
}

sonarqube {
    properties {
        property "sonar.sourceEncoding", "UTF-8"
    }
}

apply from: 'dependencies.gradle'
def env = System.getProperty("dev") ?: 'dev'

// 配置所有子项目
subprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'jacoco'

    sourceCompatibility = project_jdk
    targetCompatibility = project_jdk

    // 公共依赖
    dependencies {
        implementation 'io.rest-assured:rest-assured'
        implementation 'org.slf4j:slf4j-api'
        implementation 'ch.qos.logback:logback-classic'
        implementation 'ch.qos.logback:logback-core'
        implementation 'org.slf4j:jcl-over-slf4j'
        implementation 'com.google.guava:guava'
        testImplementation 'org.junit.jupiter:junit-jupiter-api'
        testImplementation 'org.junit.jupiter:junit-jupiter-params'
        testImplementation 'org.junit.vintage:junit-vintage-engine'
        testImplementation 'org.junit.jupiter:junit-jupiter-engine'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'org.assertj:assertj-core'
        testImplementation 'org.hamcrest:hamcrest'
        testImplementation 'org.hamcrest:hamcrest-all'
        implementation 'org.projectlombok:lombok'
        testImplementation 'org.mockito:mockito-core'
        testImplementation 'org.apache.maven.surefire:surefire-logger-api'

        implementation "com.h2database:h2:${h2}"

        compileOnly "org.projectlombok:lombok:${lombok}"
        annotationProcessor "org.projectlombok:lombok:${lombok}"
        testCompileOnly "org.projectlombok:lombok:${lombok}"
        testAnnotationProcessor "org.projectlombok:lombok:${lombok}"
    }

    dependencyManagement {
        dependencies {
            for (depJar in rootProject.ext.dependencies) {
                dependency depJar.value
            }
        }
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springboot}"
        }
    }

    sourceSets {
        main {
            java { srcDirs = ['src/main/java'] }
            resources { srcDirs = ['src/main/resources', "src/main/profiles/$env"] }
        }
        test {
            java { srcDirs = ['src/test/java', 'src/test/groovy'] }
            resources { srcDirs = ['src/test/resources'] }
        }
    }

    test {
        useJUnitPlatform()
        finalizedBy jacocoTestReport // report is always generated after tests run
    }

    jacocoTestReport {
        dependsOn test // tests are required to run before generating the report
        reports {
            xml.required = true
        }
    }

    jacoco {
        toolVersion = '0.8.8'
    }

    [compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'
}